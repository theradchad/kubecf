package(default_visibility = ["//visibility:public"])

load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")
load("//deploy/helm/kubecf:defs.bzl", "metadata_file_generator")
load("//rules/helm:def.bzl", helm_package = "package")
load("//rules/yaml_extractor:defs.bzl", "yaml_extractor")
load("//rules/gomplate:def.bzl", "gomplate")
load("//:def.bzl", "project")

metadata_file_generator(
    name = "metadata",
    file = "Metadata.yaml",
    operator_chart = project.cf_operator.chart.url,
)

filegroup(
    name = "chart_files_static",
    srcs = glob(
        ["**/*"],
        exclude = [
            "**/BUILD.bazel",
            "**/defs.bzl",
            "templates/*.yaml",
        ],
    ),
)

[gomplate(
    name = "templated_{}".format(file),
    src = file,
    left_delim = "<<",
    right_delim = ">>",
    data = {
        "images": Label("//deploy/containers/credhub-setup:credhub-setup-bundle"),
    },
) for file in glob(["templates/*.yaml"])]

pkg_tar(
    name = "chart_files_templated",
    strip_prefix = "./",
    srcs = ["templated_{}".format(f) for f in glob(["templates/*.yaml"])]
)

pkg_tar(
    name = "cf_deployment",
    package_dir = "assets",
    srcs = [
        "@cf_deployment//:cf_deployment",
    ],
)

yaml_extractor(
    name = "auctioneer_job",
    src = "@cf_deployment//:cf-deployment.yml",
    filter = """.instance_groups[] | select(.name == "scheduler") | .jobs[] | select(.name == "auctioneer")""",
)

yaml_extractor(
    name = "routing_api_job",
    src = "@cf_deployment//:cf-deployment.yml",
    filter = """.instance_groups[] | select(.name == "api") | .jobs[] | select(.name == "routing-api")""",
)

pkg_tar(
    name = "extracted_jobs",
    package_dir = "assets/jobs",
    srcs = [
        ":auctioneer_job",
        ":routing_api_job",
    ],
)

helm_package(
    name = "chart",
    srcs = [
        ":chart_files_static",
    ],
    generated = [
        ":metadata",
    ],
    tars = [
        "//bosh/releases:pre_render_scripts",
        ":cf_deployment",
        ":extracted_jobs",
        ":chart_files_templated",
    ],
    chart_name = "kubecf",
    chart_version = project.chart_version,
    app_version = project.app_version,
)
